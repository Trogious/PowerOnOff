apply plugin: 'com.android.application'

def getAppVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    def commitId = stdout.toString().replace("\n", "").replace("\r", "").trim()
    stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'tag', '--points-at', commitId
        standardOutput = stdout
    }
    def tagName = stdout.toString().replace("\n", "").replace("\r", "").trim()
    def versionName = 'git-' + commitId
    if (tagName != null && "" != tagName) {
        versionName = tagName
    }
    return versionName
}

android {
    signingConfigs {
        release {
            keyAlias 'upload'
            keyPassword "$System.env.POOFF_APKSIGN_PASS"
            storeFile file('./certs/apksign.jks')
            storePassword "$System.env.POOFF_APKSIGN_PASS"
        }
    }
    compileSdkVersion 26
    buildToolsVersion '29.0.3'
    defaultConfig {
        applicationId "net.swmud.trog.poweronoff"
        minSdkVersion 18
        targetSdkVersion 26
        versionCode 1
        versionName "0.1"
        archivesBaseName = "${getProject().getName()}-$versionName-$versionCode-${getAppVersion()}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "APP_VERSION", "\"${getAppVersion()}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    testImplementation 'junit:junit:4.13'
}
